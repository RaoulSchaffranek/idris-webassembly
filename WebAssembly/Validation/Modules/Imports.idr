module WebAssembly.Validation.Modules.Imports

import WebAssembly.Structure
import WebAssembly.Validation.Conventions
import WebAssembly.Validation.Types

||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#imports)
public export
data ValidImportDesc : Context -> ImportDesc -> ExternType -> Type where
  
  ||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#xref-syntax-modules-syntax-importdesc-mathsf-func-x)
  |||
  ||| ```
  ||| C.types[x] = [t1*] -> [t2*]
  ||| --------------------------------
  ||| C âŠ¢ func x : func [t1*] -> [t2*]
  ||| ```
  ValidImportFunc  : (c : Context)
                  -> (x : TypeIdx)
                  -> {auto in_bounds: InBounds x (types c)}
                  -> ValidImportDesc c (FuncImport x) (Func (index x (types c)))

  ||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#xref-syntax-modules-syntax-importdesc-mathsf-table-xref-syntax-types-syntax-tabletype-mathit-tabletype)
  |||
  ||| ```
  ||| tabletype ok
  ||| -------------------------------------
  ||| C âŠ¢ table tabletype : table tabletype
  ||| ```
  MkValidImportTable : (c : Context)
                    -> (tabletype : TableType)
                    -> ValidTableType tabletype
                    -> ValidImportDesc c (TableImport tabletype) (Table tabletype)

  ||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#xref-syntax-modules-syntax-importdesc-mathsf-mem-xref-syntax-types-syntax-memtype-mathit-memtype)
  |||
  ||| ```
  ||| memtype ok
  ||| -----------------------------
  ||| C âŠ¢ mem memtype : mem memtype
  ||| ```
  MkValidImportMem : (c : Context)
                  -> (memtype : MemType)
                  -> ValidMemoryType memtype
                  -> ValidImportDesc c (MemImport memtype) (Mem memtype)

  ||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#xref-syntax-modules-syntax-importdesc-mathsf-global-xref-syntax-types-syntax-globaltype-mathit-globaltype)
  |||
  ||| ```
  ||| globaltype ok
  ||| -----------------------------
  ||| C âŠ¢ global globaltype : global globaltype
  ||| ```
  MkValidImportGlobal  : (c : Context)
                      -> (globaltype : GlobalType)
                      -> ValidGlobalType globaltype
                      -> ValidImportDesc c (GlobalImport globaltype) (Global globaltype)

||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#imports)
public export
data ValidImport : Context -> Import -> ExternType -> Type where
  ||| [ðŸ”— Spec](https://webassembly.github.io/spec/core/valid/modules.html#xref-syntax-modules-syntax-import-mathsf-module-xref-syntax-values-syntax-name-mathit-name-1-xref-syntax-modules-syntax-import-mathsf-name-xref-syntax-values-syntax-name-mathit-name-2-xref-syntax-modules-syntax-import-mathsf-desc-xref-syntax-modules-syntax-importdesc-mathit-importdesc)
  |||
  ||| ```
  ||| C âŠ¢ importdesc : externtype
  ||| ------------------------------------------------------------
  ||| C âŠ¢ {module name1, name name2, desc importdesc} : externtype
  ||| ```
  MkValidImport  : (c : Context)
                -> (name1 : Name)
                -> (name2 : Name)
                -> (importdesc : ImportDesc)
                -> (externtype : ExternType)
                -> ValidImportDesc c importdesc externtype
                -> ValidImport c (MkImport name1 name2 importdesc) externtype
